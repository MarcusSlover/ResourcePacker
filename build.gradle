/*
 * MIT License
 *
 * Copyright (c) 2022 MarcusSlover
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.marcusslover'
version '1.3-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.build.dependsOn tasks.shadowJar

tasks.withType(JavaCompile) {
    options.warnings = false
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'net.lingala.zip4j:zip4j:2.9.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveFileName = "ResourcePacker-${version}.jar"

    manifest {
        attributes('Implementation-Title': 'ResourcePacker',
                'Implementation-Version': version,
                'Main-Class': 'com.marcusslover.resourcepacker.core.packer.Core'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.marcusslover'
            artifactId = 'resourcepacker'
            from components.java
            pom {
                name = 'Resource Packer'
                description = 'Aimed to easily create Minecraft resource packs with custom items and blocks.'
                url = 'https://github.com/MarcusSlover/ResourcePacker'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/MarcusSlover/ResourcePacker/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'marcusslover'
                        name = 'MarcusSlover'
                        email = 'jr.marcusorlando@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials(PasswordCredentials)
        }
    }

}


